apply plugin: 'java'

repositories {
  mavenCentral()

  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
}

configurations {
  provided
    compile.extendsFrom provided
}

dependencies {
  compile 'com.google.guava:guava:18.0'
  compile 'org.pircbotx:pircbotx:2.1-SNAPSHOT'
  compile 'org.slf4j:slf4j-simple:1.7.7'
  compile 'org.apache.commons:commons-lang3:3.3.2'
  compile 'commons-codec:commons-codec:1.9'
  compile 'org.yaml:snakeyaml:1.14'
  compile 'mysql:mysql-connector-java:5.1.6'
}

sourceCompatibility = 1.7
version = '0.0.3-indev'

jar {
  dependsOn configurations.runtime
  from {
    (configurations.runtime - configurations.provided).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
  manifest {
    attributes 'Implementation-Title': 'IRCbutt',
               'Implementation-Version': version,
               'Main-Class': 'net.alureon.ircbutt.Main'
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [ '-Xlint:all', '-Xlint:-path' ]
  options.deprecation = true
  options.encoding = 'utf8'
}

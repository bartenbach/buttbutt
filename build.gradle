plugins {
  id 'io.spring.dependency-management' version '1.0.1.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

dependencyManagement {
  imports {
    mavenBom 'org.apache.logging.log4j:log4j-bom:2.10.0'
  }
}

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    testCompile 'junit:junit:4.12-beta-3'
    //yes, we need 3 jars for logging.  don't ask me about it.  I don't want to talk about it.
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.10.0'
    compile 'org.pircbotx:pircbotx:2.1'
    compile 'org.yaml:snakeyaml:1.14'
    compile 'mysql:mysql-connector-java:5.1.36'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'net.sourceforge.jeval:jeval:0.9.4'
}

sourceCompatibility = 1.8
version = '0.4.6-indev'

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes 'Implementation-Title': 'IRCbutt',
                'Implementation-Version': version,
                'Implementation-Vendor': 'http://github.com/proxa/IRCbutt',
                'Main-Class': 'net.alureon.ircbutt.Main',
                'Built-By': System.properties['user.name']
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'utf8'
}

tasks.withType(Checkstyle) {
    configFile = new File('config/checkstyle/sane_checks.xml')
}

// more informative tests
test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
